PRO easegrid,iopt,alat,alon,thelon,thelat,ascale
;
;	COMPUTES THE FORWARD "EASE1" GRID TRANSFORM
;
;	GIVEN A LAT,LON (ALAT,ALON) AND THE SCALE (ASCALE) THE IMAGE
;	TRANSFORMATION COORDINATES (THELON,THELAT) ARE COMUTED
;	USING THE "EASE GRID" (VERSION 1.0) TRANSFORMATION GIVEN IN FORTRAN
;	SOURCE CODE SUPPLIED BY NSIDC.
;
;	THE RADIUS OF THE EARTH USED IN THIS PROJECTION IS IMBEDDED INTO
;	ASCALE WHILE THE PIXEL DIMENSION IN KM IS IMBEDDED IN BSCALE
;	THE BASE VALUES ARE: RADIUS EARTH= 6371.228 KM
;			     PIXEL DIMEN =25.067525 KM
;	THEN, BSCALE = BASE_PIXEL_DIMEN
;	      ASCALE = RADIUS_EARTH/BASE_PIXEL_DIMEN
;
;	THESE VALUES ARE SET IN THE GETSIR AND PUTSIR ROUTINES
;
;	IOPT IS EASE TYPE: IOPT=11=NORTH, IOPT=12=SOUTH, IOPT=13=CYLINDRICAL
;

; corrected and revised by dgl 25 jul 2005
	PI2=1.57079633D0			; Pi/2
	DTR=PI2/90.0D0

	IF IOPT EQ 11 THEN BEGIN		; EASE GRID NORTH
		THELON= ASCALE*SIN(ALON*DTR)*SIN(DTR*(45.0-0.5*ALAT))
		THELAT=-ASCALE*COS(ALON*DTR)*SIN(DTR*(45.0-0.5*ALAT))
	ENDIF ELSE IF IOPT EQ 12 THEN BEGIN	; EASE GRID SOUTH
		THELON=ASCALE*SIN(ALON*DTR)*COS(DTR*(45.0-0.5*ALAT))
		THELAT=ASCALE*COS(ALON*DTR)*COS(DTR*(45.0-0.5*ALAT))
	ENDIF ELSE IF IOPT EQ 13 THEN BEGIN	; EASE CYLINDRICAL
		THELON=ASCALE*PI2*ALON*COS(30.0*DTR)/90.0
		THELAT=ASCALE*SIN(ALAT*DTR)/COS(30.0*DTR)
	ENDIF
	END
