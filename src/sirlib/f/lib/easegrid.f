	SUBROUTINE EASEGRID(IOPT,ALAT,ALON,THELON,THELAT,ASCALE)
C
C	COMPUTES THE FORWARD "EASE1" GRID TRANSFORM
C
C	GIVEN A LAT,LON (ALAT,ALON) AND THE SCALE (ASCALE) THE IMAGE
C	TRANSFORMATION COORDINATES (THELON,THELAT) ARE COMUTED
C	USING THE "EASE GRID" (VERSION 1.0) TRANSFORMATION GIVEN IN FORTRAN
C	SOURCE CODE SUPPLIED BY NSIDC.
C
C	THE RADIUS OF THE EARTH USED IN THIS PROJECTION IS IMBEDDED INTO
C	ASCALE WHILE THE PIXEL DIMENSION IN KM IS IMBEDDED IN BSCALE
C	THE BASE VALUES ARE: RADIUS EARTH= 6371.228 KM
C			     PIXEL DIMEN =25.067525 KM
C	THEN, BSCALE = BASE_PIXEL_DIMEN
C	      ASCALE = RADIUS_EARTH/BASE_PIXEL_DIMENSION
C
C	IOPT IS EASE1 TYPE: IOPT=11=NORTH, IOPT=12=SOUTH, IOPT=13=CYLINDRICAL
C
C	WRITTEN BY: DGL 4 MAR 1995
C       MODIFIED BY: DGL 23 JUL 2005 
C       REVISED BY: DGL NOV 2005
C        + eliminated COSD and SIND calls with DTR
C
	IMPLICIT NONE
	INTEGER IOPT
	REAL ALAT, ALON, THELON, THELAT, ASCALE
	REAL PI2
	REAL DTR
	DATA DTR/0.01745329252/
	DATA PI2/1.57079633/		! Pi/2
C
	IF (IOPT.EQ.11) THEN		! EASE GRID NORTH
		THELON= ASCALE*SIN(DTR*ALON)*SIN(DTR*(45.0-0.5*ALAT))
		THELAT=-ASCALE*COS(DTR*ALON)*SIN(DTR*(45.0-0.5*ALAT))
	ELSE IF (IOPT.EQ.12) THEN	! EASE GRID SOUTH
		THELON=ASCALE*SIN(DTR*ALON)*COS(DTR*(45.0-0.5*ALAT))
		THELAT=ASCALE*COS(DTR*ALON)*COS(DTR*(45.0-0.5*ALAT))
	ELSE IF (IOPT.EQ.13) THEN	! EASE CYLINDRICAL
		THELON=ASCALE*PI2*ALON*COS(DTR*30.0)/90.0
		THELAT=ASCALE*SIN(DTR*ALAT)/COS(DTR*30.0)
	ENDIF
	RETURN
	END
