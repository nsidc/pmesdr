PRO polster,alon,alat,x1,y1,xlam,slat
;
;	COMPUTES THE POLAR STEROGRAPHIC TRANSFORMATION FOR A LON,LAT
;	INPUT OF (ALON,ALAT) WITH REFERENCE ORIGIN  LON,LAT=(XLAM,SLAT).
;	OUTPUT IS (X1,Y1) IN KM
;
;	ALGORITHM IS THE SAME AS USED FOR PROCESSING ERS-1 SAR IMAGES
;	AS RECEIVED FROM M. DRINKWATER (1994) 
;

;	DGL 7/14/98: fixed sign error in computing CM
;
	E2=0.006693883D0
	RE=6378.273D0
	DTR=3.141592654/180.0
	E=SQRT(E2)
	IF SLAT LT 0.0 THEN BEGIN
		SN=-1.0
		RLAT=-ALAT
	ENDIF ELSE BEGIN
		SN=1.0
		RLAT=ALAT
	ENDELSE
	T=((1.D0-E*SIN(RLAT*DTR))/(1.D0+E*SIN(RLAT*DTR)))^(E*0.5D0)
	TY=TAN(DTR*(45.0D0-0.5D0*RLAT))/T
	IF SLAT LT 0.0 THEN RLAT=-SLAT ELSE RLAT=SLAT
	IF ABS(SLAT) EQ 90.0 THEN PRINT,"*** Invalid reference latitude in polster ***",SLAT
	T=((1.D0-E*SIN(DTR*RLAT))/(1.D0+E*SIN(DTR*RLAT)))^(E*0.5D0)
	TX=TAN(DTR*(45.0D0-0.5D0*RLAT))/T
	CM=COS(DTR*RLAT)/SQRT(1.D0-E2*SIN(DTR*RLAT)^2)
	RHO=RE*CM*TY/TX
	X1= (SN*SIN(DTR*(SN*ALON-XLAM)))*RHO
	Y1=-(SN*COS(DTR*(SN*ALON-XLAM)))*RHO
RETURN
END
