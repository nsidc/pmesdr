PRO ilambert1,LAT,ALON,X,Y,ORGLAT,ORGLON,IOPT
;
;	COMPUTES THE INVERSE TRANSFORMATION FROM LAT/LON TO X/Y FOR THE
;	LAMBERT AZIMUTHAL EQUAL-AREA PROJECTION
;
;	SEE "MAP PROJECTIONS USED BY THE U.S. GEOLOGICAL SURVEY"
;	GEOLOGICAL SURVEY BULLETIN 1532, PGS 157-173
;
;	FOR THIS ROUTINE, A SPHERICAL EARTH IS ASSUMED FOR THE PROJECTION.
;	THE ERROR WILL BE SMALL FOR SMALL-SCALE MAPS.  
;	FOR IOPT=1 A FIXED, NOMINAL EARTH RADIUS IS USED.
;	FOR IOPT=2 THE LOCAL RADIUS OF THE EARTH IS USED BASED ON
;	THE 1972 WGS ELLIPSOID MODEL (BULLETIN PG 15).
;
;	INPUTS:
;	 LAT	(R): LATITUDE +90 TO -90 DEG WITH NORTH POSITIVE
;	 LON	(R): LONGITUDE 0 TO +360 DEG WITH EAST POSITIVE
;			OR -180 TO +180 WITH EAST MORE POSITIVE
;	 ORGLAT	(R): ORIGIN PARALLEL +90 TO -90 DEG WITH NORTH POSITIVE
;	 ORGLON	(R): CENTRAL MERIDIAN (LONGITUDE) 0 TO +360 DEG
;			OR -180 TO +180 WITH EAST MORE POSITIVE
;	 IOPT	(I): EARTH RADIUS OPTION
;
;	OUTPUTS:
;	 X,Y	(R): RECTANGULAR COORDINATES IN KM
;
	DTR=3.141592654D0/180.0
	RADEARTH=6378.135D0	; EQUITORIAL EARTH RADIUS
	F=298.26D0		; 1/f
	ORGLON1=(ORGLON+720.0) MOD 360.0
;
;	COMPUTE LOCAL RADIUS OF THE EARTH AT CENTER OF IMAGE
;
	ERADEARTH=6378.0D0	;  USE FIXED NOMINAL VALUE
	IF IOPT EQ 2 THEN BEGIN ;  USE LOCAL RADIUS
	  ERA=(1.-1./F)
	  ERADEARTH=RADEARTH*ERA/SQRT(ERA*ERA*COS(ORGLAT*dtr)^2+SIN(ORGLAT*dtr)^2)
	ENDIF
	X1=X/ERADEARTH
	Y1=Y/ERADEARTH
	RHO=X1*X1+Y1*Y1
	IF RHO GT 0.0 THEN BEGIN
	  RHO=SQRT(RHO)
	  C=2*ASIN(RHO*0.5)
	  LAT=ASIN(COS(C)*SIN(ORGLAT*dtr)+Y1*SIN(C)*COS(ORGLAT*dtr)/RHO)/dtr
	ENDIF ELSE LAT=ORGLAT

	LON=0
	IF ABS(ORGLAT) NE 90.0 THEN BEGIN
	  IF RHO EQ 0.0 THEN LON=ORGLON1 ELSE BEGIN
	    T1=X1*SIN(C)
	    T2=RHO*COS(ORGLAT*dtr)*COS(C)-Y1*SIN(ORGLAT*dtr)*SIN(C)
	    LON=ORGLON1+ATAN(T1,T2)/dtr
	  ENDELSE 
	ENDIF ELSE IF ORGLAT EQ 90.0 THEN LON=ORGLON1+ATAN(X1,-Y1)/dtr ELSE $
		LON=ORGLON1+ATAN(X1,Y1)/dtr
	LON=(LON+720.0) mod 360.0
	IF LON GT 180.0 THEN LON=LON-360.0
	ALON=LON
	END



